plugins {
    id 'base'
    id 'org.asciidoctor.jvm.pdf' version '3.3.0' apply false
    id 'org.asciidoctor.jvm.gems' version '3.3.0' apply false
    id 'org.asciidoctor.jvm.revealjs' version '3.3.0' apply false
}

allprojects {
    version = '1.0.0-SNAPSHOT'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.asciidoctor.jvm.pdf'
    apply plugin: 'org.asciidoctor.jvm.gems'
    apply plugin: 'org.asciidoctor.jvm.revealjs'

    repositories {
        mavenCentral()
        ruby.gems()
    }

    dependencies {
        asciidoctorGems 'rubygems:rouge:3.15.0'
    }

    asciidoctorPdf {
        sourceDir file("docs")

        dependsOn asciidoctorGemsPrepare

        baseDirFollowsSourceFile()

        asciidoctorj {
            requires 'rouge'
        }

        attributes(
                'sourcedir': project.sourceSets.main.java.srcDirs[0],
                'endpoint-url': 'http://example.org',
                'source-highlighter': 'rouge',
                'imagesdir': '../resources/images',
                'revnumber': { project.version.toString() },
                'toc': 'left',
                'icons': 'font',
                'idprefix': '',
                'idseparator': '-',
        )
    }

    asciidoctorRevealJs {
        sourceDir file("slides")

        baseDirFollowsSourceFile()

        resources {
            from("resources") {
                include '**'
                into '.'
            }
        }

        attributes(
                'endpoint-url': 'http://example.org',
                'source-highlighter': 'coderay',
                'coderay-css': 'style',
                'imagesdir': './images',
                'toc': 'left',
                'icons': 'font',
                'setanchors': '',
                'idprefix': 'slide-',
                'idseparator': '-',
                'docinfo': 'shared',
                'revealjs_theme': 'black',
                'revealjs_transition': 'linear',
                'revealjs_history': 'true',
                'revealjs_slideNumber': 'true',
                'revnumber': { project.version.toString() },
        )
    }

    task asciidoctor(dependsOn: [asciidoctorPdf, asciidoctorRevealJs])

    tasks.register('buildDistributionZip', Zip) {
        dependsOn asciidoctor
        from project.layout.buildDirectory.dir("docs/asciidocPdf")
        from project.layout.buildDirectory.dir("docs/asciidocRevealJs")
    }
}

task copyArtifactsForDistributionZip(description: 'Copies the artifacts that will be included in the distribution zip file') {
    dependsOn subprojects.collect { "${it.name}:asciidoctor" }
    def artifactDir = file("${buildDir}/toArchive")
    outputs.dir artifactDir

    doLast {
        subprojects.each { project ->
            def projectDir = project.file("${artifactDir}/${project.name}")
            copy {
                from project.layout.buildDirectory.dir("docs/asciidocPdf")
                from project.layout.buildDirectory.dir("docs/asciidocRevealJs")
                into projectDir
            }
        }
    }
}
task buildDistributionZip(type: Zip) {
    dependsOn 'copyArtifactsForDistributionZip'
    from copyArtifactsForDistributionZip.outputs.files
}