plugins {
    id 'org.asciidoctor.jvm.pdf' version '3.3.0'
    id 'org.asciidoctor.jvm.gems' version '3.3.0'
    id 'org.asciidoctor.jvm.revealjs' version '3.3.0'
}

apply plugin: 'java'

version = '1.0.0-SNAPSHOT'

repositories {
    jcenter()
    ruby.gems()
}

configurations {
    pdf
}

dependencies {
    asciidoctorGems 'rubygems:rouge:3.15.0'
    pdf 'me.champeau.deck2pdf:deck2pdf:0.3.0'
}

asciidoctorPdf {
    sourceDir file("src/docs")
    sourceDir file("src/slides")

    dependsOn asciidoctorGemsPrepare

    baseDirFollowsSourceFile()

    asciidoctorj {
        requires 'rouge'
        attributes 'build-gradle': file('build.gradle'),
                'sourcedir': project.sourceSets.main.java.srcDirs[0],
                'endpoint-url': 'http://example.org',
                'source-highlighter': 'rouge',
                'imagesdir': '../resources/images',
                'toc': 'left',
                'icons': 'font',
                'idprefix': '',
                'idseparator': '-'
    }
}

asciidoctorRevealJs {
    sourceDir file("src/slides")

    baseDirFollowsSourceFile()

    resources {
        from("src/resources") {
            include '**'
            into '.'
        }
    }

    attributes 'build-gradle': file('build.gradle'),
            'endpoint-url': 'http://example.org',
            'source-highlighter': 'coderay',
            'coderay-css': 'style',
            'imagesdir': './images',
            'toc': 'left',
            'icons': 'font',
            'setanchors': '',
            'idprefix': 'slide-',
            'idseparator': '-',
            'docinfo': 'shared',
            'revealjs_theme': 'black',
            'revealjs_transition': 'linear',
            'revealjs_history': 'true',
            'revealjs_slideNumber': 'true'
}

// alias
task asciidoctor(dependsOn: [asciidoctorPdf, asciidoctorRevealJs])

task generatePdf(type:JavaExec) {
    dependsOn asciidoctor
    main = 'me.champeau.deck2pdf.Main'
    workingDir = file("$buildDir/docs/asciidocRevealJs/")
    args = ["$workingDir/example-deck.html","$workingDir../asciiDocPdf/example-deck.pdf", '--profile=revealjs']
    classpath = configurations.pdf
}